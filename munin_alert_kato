#!/usr/bin/perl
# Copyright (c) 2011, Distilled Media Ltd.
# Copyright (c) 2013, LeChat Inc.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# - Redistributions of source code must retain the above copyright notice,
#   this list of conditions and the following disclaimer.
# - Redistributions in binary form must reproduce the above copyright notice,
#   this list of conditions and the following disclaimer in the documentation
#   and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.

#
# This script converts Munin alerts into messages in a Kato room.
#
# Put this script somewhere on your Munin master and make it executable. Then
# add it as a contact in your Munin configuration like so:
#
# contact.kato.command [THIS] --kato_url=[KATO_URL]
# contact.kato.always_send warning critical
#
# Replace [THIS] with the full path to this script. Replace [KATO_URL] with url
# provided in integrations tab in your room setttings.
#
# Don't forget to add it to your contacts list.
#
# If you want to change the name associated with the message, pass a value for
# the --from argument. If you'd like the messages to link back to your Munin
# web interface then use --munin-prefix.
#

use strict;
use warnings;

use Getopt::Long;
use HTML::Entities;
use HTTP::Request::Common qw(POST);
use LWP::UserAgent;

# Command line arguments
my $KATO_FROM = 'Munin';
my $KATO_URL = '';
my $MUNIN_URL_PREFIX = '';

# Fetch the command line arguments
&usage unless GetOptions(
	'from=s' => \$KATO_FROM,
	'kato_url=s' => \$KATO_URL,
	'munin-prefix=s' => \$MUNIN_URL_PREFIX
);

# Enforce the mandatory arguments
&usage unless $KATO_URL;

# A bodge to allow "special" characters in the -from and --kato_url parameters
# since it appears to be impossible to pass them safely from munin.conf.
$KATO_FROM =~ s/\%([A-Fa-f0-9]{2})/pack('C', hex($1))/seg;
$KATO_URL =~ s/\%([A-Fa-f0-9]{2})/pack('C', hex($1))/seg;

# Read the data from Munin
my $alert_data = '';
while (<STDIN>) {
	$alert_data .= $_;
}

# Parse the alerts and send messages about them.
my @alerts = split /(?:\r\n|\r|\n){2}/o, $alert_data;
if (@alerts) {
	my $kato = new LWP::UserAgent();
	foreach (@alerts) {
		&send_to_kato($kato, &format_for_kato($_));
	}
}

# Format an alert for Kato
sub format_for_kato {
	# Encode the basic entities and then split for processing
	my @alert = split /(?:\r\n|\r|\n)/o, encode_entities(shift);

	# Re-format the first line.
	if ($MUNIN_URL_PREFIX) {
		$alert[0] =~ s#^(.*?) :: (.*?) :: #<a href="$MUNIN_URL_PREFIX/$1/$2/">$2</a><br />#o;
	} else {
		$alert[0] =~ s#^(.*?) :: (.*?) :: #<b>$2</b><br />#o;
	}

	# Figure out the highest alert level so that we can work out the colour it
	# should be. While we're at it, re-format each line.
	my $alert_level = 0;
	my $alert_level_label = 'OK';
	my $alerted_items = '';
	for (my $i = 1; $i < @alert; $i++) {
		if ($alert[$i] =~ /^\s+CRITICALs: (.*)$/o) {
			$alert_level = ($alert_level > 2 ? $alert_level : 2);
			$alert_level_label = 'CRITICAL';
			$alerted_items = $1;
		} elsif ($alert[$i] =~ /^\s+WARNINGs: (.*)$/o) {
			$alert_level = ($alert_level > 1 ? $alert_level : 1);
			$alert_level_label = 'WARNING';
			$alerted_items = $1;
		} elsif ($alert[$i] =~ /^\s+UNKNOWNs: (.*)$/o) {
			$alert_level = ($alert_level > 1 ? $alert_level : 1);
			$alert_level_label = 'UNKNOWN';
			$alerted_items = $1;
		} elsif ($alert[$i] =~ /^\s+OKs: (.*)$/o) {
			$alert_level = ($alert_level > 0 ? $alert_level : 0);
			$alert_level_label = 'OK';
			$alerted_items = $1;
		}

		my @alerted_items = split /, /o, $alerted_items;
		$alert[$i] = '';
		foreach (@alerted_items) {
			$alert[$i] .= "&nbsp;&nbsp;&nbsp;&nbsp;$alert_level_label: $_<br />";
		}
	}

	# Convert the alert level to a colour.
	if ($alert_level >= 2) {
		$alert_level = "red";
	} elsif ($alert_level == 1) {
		$alert_level = "yellow";
	} else {
		$alert_level = "green";
	}

	return {
		'message' => join('<br />', @alert),
		'color' => $alert_level,
	};
}

# Send an alert as a message to a Kato room
sub send_to_kato {
	my $kato = shift;
	my $alert = shift;

	$alert->{'from'} = $KATO_FROM;

	my $req = POST $KATO_URL, $alert;
	$kato->request($req);
}

# Print out a usage message.
sub usage {
	print "Usage: $0 [--from=Munin] --kato_url=https://api.kato.im/1234abcd [--munin-prefix=https://munin.example.com]\n";
	exit(0);
}
